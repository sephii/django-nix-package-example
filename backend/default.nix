{ poetry, poetry2nix, example_project, mkShell, python3, stdenv, writeShellScriptBin }:
let
  # Some packages have specific dependencies poetry2nix doesn’t seem to be aware of
  # If you get errors while building the main package, check which Python package is causing the problem and add any
  # missing dependency here
  poetryOverrides = poetry2nix.overrides.withDefaults (self: super: {
    # https://github.com/nix-community/poetry2nix/issues/568
    anyascii = super.anyascii.overridePythonAttrs (old: {
      buildInputs = old.buildInputs or [ ] ++ [ self.flit-core ];
    });

    django-debug-toolbar = super.django-debug-toolbar.overridePythonAttrs (old: {
      buildInputs = old.buildInputs or [ ] ++ [ self.hatchling ];
    });

    django-vite = super.django-vite.overridePythonAttrs (old: {
      buildInputs = old.buildInputs or [ ] ++ [ self.setuptools ];
    });
  });

  # The main app that is used when not in dev mode (for dev mode, see the `shell` derivation below)
  poetryApp = (poetry2nix.mkPoetryApplication {
    projectDir = ./.;
    # Copy static files generated by the frontend build process to our static files directory, so the files are then
    # picked up by the `collectstatic` phase (see the `static` derivation below)
    configurePhase = ''
      mkdir -p ./example_project/static
      cp -r ${example_project.frontend.static}/* ./example_project/static
    '';
    overrides = poetryOverrides;
    python = python3;
    propagatedBuildInputs = [ python3.pkgs.setuptools ];
  });

  # This is a derivation with all the static files resulting from Django’s `collectstatic`
  static = stdenv.mkDerivation {
    pname = "${poetryApp.pname}-static";
    version = poetryApp.version;
    src = ./.;
    buildPhase = ''
      export STATIC_ROOT=$out
      export MEDIA_ROOT=/dev/null
      export DJANGO_SETTINGS_MODULE=example_project.config.settings.base
      export SECRET_KEY=dummy
      export DATABASE_URL=sqlite:////dev/null
      export STATICFILES_DIRS=${example_project.frontend.static}
      ${poetryApp.dependencyEnv}/bin/django-admin collectstatic --noinput
    '';
    phases = [ "buildPhase" ];
  };
in {
  inherit static;

  server = poetryApp;

  # That’s the environment used in development
  shell = mkShell {
    packages = [
      (poetry2nix.mkPoetryEnv {
        projectDir = ./.;
        editablePackageSources = { example_project = ./example_project; };
        overrides = poetryOverrides;
      })
      python3.pkgs.poetry
    ];

    # This allows running `django-admin` from the main directory without having to first cd into backend
    shellHook = ''
      export PYTHONPATH=$PYTHONPATH:$(git rev-parse --show-toplevel)/backend
    '';
  };
}
